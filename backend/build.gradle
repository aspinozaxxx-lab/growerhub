plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.6'
    id 'io.spring.dependency-management' version '1.1.6'
}

group = 'growerhub-backend'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.5'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.rest-assured:rest-assured'
    testRuntimeOnly 'com.h2database:h2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

def bomStrippedMainDir = layout.buildDirectory.dir("bom-stripped/main")
def bomStrippedTestDir = layout.buildDirectory.dir("bom-stripped/test")

tasks.register('stripBomMain', Copy) {
    from 'src/main/java'
    into bomStrippedMainDir
    filter { line -> line.replace('\uFEFF', '') }
}

tasks.register('stripBomTest', Copy) {
    from 'src/test/java'
    into bomStrippedTestDir
    filter { line -> line.replace('\uFEFF', '') }
}

tasks.named('compileJava') {
    dependsOn tasks.named('stripBomMain')
    source = fileTree(bomStrippedMainDir)
}

tasks.named('compileTestJava') {
    dependsOn tasks.named('stripBomTest')
    source = fileTree(bomStrippedTestDir)
}

bootJar {
    archiveFileName = "app.jar"
}

jar {
    enabled = false
}
